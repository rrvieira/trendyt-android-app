plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

Properties properties = new Properties()
if (rootProject.file("local.properties").exists()) {
    properties.load(rootProject.file("local.properties").newDataInputStream())
}
String systemTmdbApiKey = System.getenv("TMDB_API_KEY") ?: ""
String tmdbApiKey = properties.getProperty("tmdb.api.key", systemTmdbApiKey)

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.rrvieira.trendyt"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "0.1"

        testInstrumentationRunner "com.rrvieira.trendyt.TrendytTestRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        buildConfigField "String", "TMDB_API_URL", "\"https://api.themoviedb.org/3/\""
        buildConfigField "String", "TMDB_API_KEY", "\"$tmdbApiKey\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
        }
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.3.0"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    //core
    implementation 'androidx.core:core-ktx:1.8.0'

    //compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.foundation:foundation-layout:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.material3:material3:1.0.0-beta01"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"

    //compose extensions/integration
    implementation "androidx.activity:activity-compose:1.5.1"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"
    implementation "androidx.navigation:navigation-compose:2.5.1"

    //material components
    implementation "com.google.android.material:material:1.6.1"

    //lifecycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.5.1"

    //retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit2_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit2_version"
    implementation "com.squareup.okhttp3:logging-interceptor:4.10.0"

    //coil
    implementation "io.coil-kt:coil:2.1.0"
    implementation "io.coil-kt:coil-compose:2.1.0"

    //hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_version"

    //swipe refresh
    implementation "com.google.accompanist:accompanist-swiperefresh:$accompanist_version"
    //system ui controller
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"

    //junit
    testImplementation "junit:junit:4.13.2"

    //test
    testImplementation "androidx.test:core:1.4.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"

    //mockk
    testImplementation "io.mockk:mockk:1.12.2"

    //coroutines test
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4"
}
