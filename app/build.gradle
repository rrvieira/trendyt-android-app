plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

Properties properties = new Properties()
if (rootProject.file("local.properties").exists()) {
    properties.load(rootProject.file("local.properties").newDataInputStream())
}
String systemTmdbApiKey = System.getenv("TMDB_API_KEY") ?: ""
String tmdbApiKey = properties.getProperty("tmdb.api.key", systemTmdbApiKey)

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.rrvieira.trendyt"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "0.1"

        testInstrumentationRunner "com.rrvieira.trendyt.TrendytTestRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        buildConfigField "String", "TMDB_API_URL", "\"https://api.themoviedb.org/3/\""
        buildConfigField "String", "TMDB_API_KEY", "\"$tmdbApiKey\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
        }
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.foundation:foundation-layout:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    implementation 'androidx.activity:activity-compose:1.4.0'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.3"

    implementation "io.coil-kt:coil:2.1.0"
    implementation "io.coil-kt:coil-compose:2.1.0"

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"

    // Jetpack Compose Integration
    implementation("androidx.navigation:navigation-compose:2.4.2")

    implementation 'com.google.android.material:material:1.6.1'
    implementation "androidx.compose.material3:material3:1.0.0-alpha13"
    implementation "androidx.compose.material3:material3-window-size-class:1.0.0-alpha13"

    implementation "com.google.accompanist:accompanist-swiperefresh:0.23.1"
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.23.1"

    testImplementation 'junit:junit:4.13.2'
    testImplementation "androidx.test:core:1.4.0"
    testImplementation "io.mockk:mockk:1.12.2"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.1'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"


    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_version"

    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
}
